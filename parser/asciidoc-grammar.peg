{
package parser 

import "github.com/bytesparadise/libasciidoc/types"

// *****************************************************************************************
// This file is generated after its sibling `asciidoc-grammar.peg` file. DO NOT MOIDIFY !
// *****************************************************************************************

}

Document <- lines:Line* EOF {
	return types.NewDocument(lines.([]interface{}))
}

Line <- Heading / ListItem / BlockImage / MetaElement / Inline / EmptyLine

// ------------------------------------------
// Headings
// ------------------------------------------
Heading <- level:("="+) WS+ content:Inline {
     return types.NewHeading(level, content.(*types.InlineContent))
}

// ------------------------------------------
// Lists
// ------------------------------------------
//TODO: Blank lines are required before and after a list
//TODO: Additionally, blank lines are permitted, but not required, between list items.
ListItem <- WS* ('*' / '-') WS+ content:(Inline) {
    return types.NewListItem(content.(*types.InlineContent))
}

// ------------------------------------------
// Inline content
// ------------------------------------------
Inline <- !NEWLINE elements:(QuotedText / ExternalLink / Word / WS)+ EOL {
    return types.NewInlineContent(elements.([]interface{}))
} 

// ------------------------------------------
// Quotes: bold, italic and monospace
// ------------------------------------------
QuotedText <- BoldText / ItalicText / MonospaceText

BoldText <- '*' content:(QuotedTextContent) '*' {
    return types.NewQuotedText(types.Bold, content.([]interface{}))
}

ItalicText <- '_' content:(QuotedTextContent) '_' {
    return types.NewQuotedText(types.Italic, content.([]interface{}))
}

MonospaceText <- '`' content:(QuotedTextContent) '`' {
    return types.NewQuotedText(types.Monospace, content.([]interface{}))
}

QuotedTextContent <- QuotedTextContentElement (WS+ QuotedTextContentElement)*

QuotedTextContentElement <- QuotedText / QuotedTextContentWord / InvalidQuotedTextContentWord

QuotedTextContentWord <- (!NEWLINE !WS !'*' !'_' !'`' .)+ // cannot have '*', '_' or '`' within
InvalidQuotedTextContentWord <- (!NEWLINE !WS  .)+ // can have '*', '_' or '`' within, maybe because the user made an error (extra or missing space, for example)

// ------------------------------------------
// Links
// ------------------------------------------
ExternalLink <- url:(URL_SCHEME URL) text:('[' (URL_TEXT)* ']')? {
    if text != nil {
        return types.NewExternalLink(url.([]interface{}), text.([]interface{}))
    }
    return types.NewExternalLink(url.([]interface{}), nil)
}

// ------------------------------------------
// Images
// ------------------------------------------
BlockImage <- "image::" path:(URL) '[' altText:(URL_TEXT?) ']' EOL {
    if altText != nil {
        return types.NewBlockImageWithAltText(path.(string), altText.(string))
    }
    return types.NewBlockImage(path.(string))
}

// ------------------------------------------
// meta-element types
// ------------------------------------------
MetaElement <- meta:(ElementLink / ElementID / ElementTitle) 

// a link attached to an element, such as a BlockImage
ElementLink <- "[" WS* "link" WS* "=" WS* path:URL WS* "]" EOL {
    return types.NewElementLink(path.(string))
}

// an id attached to an element, such as a BlockImage
ElementID <- "[" WS* id:(ID) WS* "]" EOL {
    return types.NewElementID(id.(string))
}

// a title attached to an element, such as a BlockImage
ElementTitle <- "." !WS title:(!NEWLINE .)+ EOL {
    return types.NewElementTitle(title.([]interface{}))
}

// ------------------------------------------
// Base types
// ------------------------------------------
Word <- (!NEWLINE !WS .)+ {
    return string(c.text), nil
}

URL <- (!NEWLINE !WS !'[' !']' .)+ {
    return string(c.text), nil
}

ID <- '#' (!NEWLINE !WS !'[' !']' .)+ {
    return string(c.text), nil
}

URL_TEXT <- (!NEWLINE !'[' !']' .)+ {
    return string(c.text), nil
}


EmptyLine <- NEWLINE {
    return types.NewEmptyLine()
}

URL_SCHEME <- "http://" / "https://" / "ftp://" / "irc://" / "mailto:"
DIGIT    <- [0-9]
NEWLINE  <- "\r\n" / '\r' / '\n'
WS       <- ' ' / '\t' {
        return string(c.text), nil
}
EOF      <- !.
EOL      <- NEWLINE / EOF