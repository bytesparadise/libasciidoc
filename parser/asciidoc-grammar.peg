{
package parser 

import "github.com/bytesparadise/libasciidoc/types"

// *****************************************************************************************
// This file is generated after its sibling `asciidoc-grammar.peg` file. DO NOT MOIDIFY !
// *****************************************************************************************

}

Document <- lines:Line* EOF {
	return types.NewDocument(lines.([]interface{}))
}

Line <- line:(Heading / ListItem / BlockImage / Inline / EmptyLine) {
    return line, nil
}

Heading <- level:("="+) WS+ content:Inline {
     return types.NewHeading(level, content.(*types.InlineContent))
}

//TODO: Blank lines are required before and after a list
//TODO: Additionally, blank lines are permitted, but not required, between list items.
ListItem <- WS* ('*' / '-') WS+ content:(Inline) {
    return types.NewListItem(content.(*types.InlineContent))
}

Inline <- !NEWLINE elements:(BoldQuote / ExternalLink / BlockImage / Word / WS)+ NEWLINE? {
    return types.NewInlineContent(elements.([]interface{}))
} 

BoldQuote <- '*' content:(BoldContent) '*' {
    return types.NewBoldQuote(content)
}

BoldContent <- (BoldContentWord WS+)* BoldContentWord {
    return string(c.text), nil
}

ExternalLink <- url:(URL_SCHEME URL) text:('[' (URL_TEXT)* ']')? {
    if text != nil {
        return types.NewExternalLink(url.([]interface{}), text.([]interface{}))
    }
    return types.NewExternalLink(url.([]interface{}), nil)

}

BlockImage <- "image::" path:(URL) altText:('[' (URL_TEXT)* ']') {
    return types.NewBlockImage(path.(string), altText.([]interface{}))
}

Word <- (!NEWLINE !WS .)+ {
    return string(c.text), nil
}

URL <- (!NEWLINE !WS !'[' !']' .)+ {
    return string(c.text), nil
}

URL_TEXT <- (!NEWLINE !'[' !']' .)+ {
    return string(c.text), nil
}

BoldContentWord <- (!NEWLINE !WS !'*' .)+ {
    return string(c.text), nil
}

EmptyLine <- NEWLINE {
    return types.NewEmptyLine()
}

URL_SCHEME <- "http://" / "https://" / "ftp://" / "irc://" / "mailto:"
DIGIT    <- [0-9]
NEWLINE  <- "\r\n" / '\r' / '\n'
WS       <- ' ' / '\t' {
        return string(c.text), nil
}
EOF      <- !.