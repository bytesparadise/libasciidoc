{
package parser 

import (
    "github.com/bytesparadise/libasciidoc/pkg/types"
    log "github.com/sirupsen/logrus"
)

// *****************************************************************************************
// This file is generated after its sibling `asciidoc-grammar.peg` file. DO NOT MODIFY !
// *****************************************************************************************

}

// ------------------------------------------
// Document
// ------------------------------------------
Document <- frontMatter:(FrontMatter?) documentHeader:(DocumentHeader?) blocks:(Section / DocumentBlock)* EOF {
	return types.NewDocument(frontMatter, documentHeader, blocks.([]interface{}))
}

DocumentBlock <- !EOF // when reaching EOF, do not try to parse a new document block again
    block:(BlankLine / DocumentAttributeDeclaration / DocumentAttributeReset / TableOfContentsMacro / List / BlockImage / LiteralBlock / DelimitedBlock / Paragraph) { // element attribute alone should be take recognized as such 
    return block, nil
}

// ------------------------------------------
// Front Matter
// ------------------------------------------
FrontMatter <- YamlFrontMatter 

FrontMatter <- YamlFrontMatterToken content:(YamlFrontMatterContent) YamlFrontMatterToken {
    return types.NewYamlFrontMatter(content.(string))
}

YamlFrontMatterToken  <- "---" EOL

YamlFrontMatterContent <- (!YamlFrontMatterToken .)* {
    return string(c.text), nil
}

// ------------------------------------------
// Document Header
// ------------------------------------------
// TODO: include main title | subtitle with support for custom separator
DocumentHeader <- header:(Section0Title) authors:(DocumentAuthors?) revision:(DocumentRevision?) otherAttributes:(DocumentAttributeDeclaration*){ 
    return types.NewDocumentHeader(header, authors, revision, otherAttributes.([]interface{}))
}

DocumentAuthors <- DocumentAuthorsInlineForm / DocumentAuthorsAttributeForm

DocumentAuthorsInlineForm <- WS* !":" authors:(DocumentAuthor+) EOL {
    return types.NewDocumentAuthors(authors.([]interface{}))
}

DocumentAuthorsAttributeForm <- WS* ":author:" author:(DocumentAuthor) {
    return []types.DocumentAuthor{author.(types.DocumentAuthor)}, nil
}

DocumentAuthor <- WS* namePart1:(DocumentAuthorNamePart) namePart2:(DocumentAuthorNamePart)? namePart3:(DocumentAuthorNamePart)? email:(DocumentAuthorEmail)? WS* ";"? WS*{
    return types.NewDocumentAuthor(namePart1, namePart2, namePart3, email)
} 

// "<" marks the beginning of the author email, and ";" is the authors separator
DocumentAuthorNamePart <- !"<" !";" Word WS*

DocumentAuthorEmail <- "<" email:(!">" !EOL .)+ ">" 

// see http://asciidoctor.org/docs/user-manual/#revision-number-date-and-remark
// Eg: "v1.0, October 2, 2013: First incarnation"
DocumentRevision <- WS* !":" revnumber:(DocumentRevisionNumber)? ","? revdate:(DocumentRevisionDate)? ":"? revremark:(DocumentRevisionRemark)? EOL {
    return types.NewDocumentRevision(revnumber, revdate, revremark)
}

// DocumentRevisionNumber MUST start with a `v` or MAY start with a `v` but MUST end with a `,`
DocumentRevisionNumber <- "v"i DIGIT  (!EOL !"," !":" .)* / "v"i? DIGIT  (!EOL !"," !":" .)* WS* &","
DocumentRevisionDate <- (!EOL !":" .)* 
DocumentRevisionRemark <- (!EOL .)*

// ------------------------------------------
// Document Attributes
// ------------------------------------------
DocumentAttributeDeclaration <- DocumentAttributeDeclarationWithNameOnly / DocumentAttributeDeclarationWithNameAndValue 

DocumentAttributeDeclarationWithNameOnly <- ":" name:(AttributeName) ":" WS* EOL {
    return types.NewDocumentAttributeDeclaration(name.([]interface{}), nil)
}

DocumentAttributeDeclarationWithNameAndValue <- ":" name:(AttributeName) ":" WS+ value:(!NEWLINE .)* EOL {
    return types.NewDocumentAttributeDeclaration(name.([]interface{}), value.([]interface{}))
}

DocumentAttributeReset <- DocumentAttributeResetWithSectionTitleBangSymbol / DocumentAttributeResetWithTrailingBangSymbol

DocumentAttributeResetWithSectionTitleBangSymbol <- ":!" name:(AttributeName) ":" WS* EOL {
    return types.NewDocumentAttributeReset(name.([]interface{}))
}

DocumentAttributeResetWithTrailingBangSymbol <- ":" name:(AttributeName) "!:" WS* EOL {
    return types.NewDocumentAttributeReset(name.([]interface{}))
}

DocumentAttributeSubstitution <- "{" name:(AttributeName) "}" {
    return types.NewDocumentAttributeSubstitution(name.([]interface{}))
}

// AttributeName must be at least one character long, 
// must begin with a word character (A-Z, a-z, 0-9 or _) and 
// must only contain word Word and hyphens ("-").
AttributeName <- ([A-Z] / [a-z] / [0-9] / "_") ([A-Z] / [a-z] / [0-9] / "-")*

// ------------------------------------------
// Table of Contents
// ------------------------------------------
TableOfContentsMacro <- "toc::[]" NEWLINE

// ------------------------------------------
// Element Attributes
// ------------------------------------------
ElementAttribute <- attr:(ElementID / ElementTitle / AdmonitionMarkerAttribute / HorizontalLayout / AttributeGroup) WS* EOL {
    return attr, nil // avoid returning something like `[]interface{}{attr, EOL}`
}

ElementID <- id:(InlineElementID) {
    return id, nil
} / "[#" id:(ID) "]" {
    return types.NewElementID(id.(string))
}

InlineElementID <- "[[" id:(ID) "]]" {
    return types.NewElementID(id.(string))
}

// a title attached to an element, such as a BlockImage (
// a title starts with a single "." followed by the value, without space in-between
ElementTitle <- "." !"." !WS title:(!NEWLINE .)+ {
    return types.NewElementTitle(title.([]interface{}))
}

// expression for the whole admonition marker, but only retains the actual kind
AdmonitionMarkerAttribute <- "[" k:(AdmonitionKind) "]" {
    return types.NewAdmonitionAttribute(k.(types.AdmonitionKind))
}

// one or more attributes. eg: [foo, key1=value1, key2=value2]other
AttributeGroup <- "[" attribute:(GenericAttribute) attributes:(OtherGenericAttribute)*  "]" {
    return types.NewAttributeGroup(append([]interface{}{attribute}, attributes.([]interface{})...))
}

GenericAttribute <- key:(AttributeKey) "=" value:(AttributeValue) { // value is set
    return types.NewGenericAttribute(key.([]interface{}), value.([]interface{}))
} / key:(AttributeKey) { // value is not set
    return types.NewGenericAttribute(key.([]interface{}), nil)
}

OtherGenericAttribute <- "," WS* key:(AttributeKey) "=" value:(AttributeValue) { // value is set
    return types.NewGenericAttribute(key.([]interface{}), value.([]interface{}))
} / "," WS* key:(AttributeKey) { // value is not set
    return types.NewGenericAttribute(key.([]interface{}), nil)
}

AttributeKey <- key:(!WS !"=" !"," !"]" .)+ WS*  {
    return key, nil
}

AttributeValue <- WS* value:(!WS !"=" !"]" .)* WS* {
    return value, nil
}

HorizontalLayout <- "[horizontal]" {
    return map[string]interface{}{"layout": "horizontal"}, nil
}

// ------------------------------------------
// Sections
// ------------------------------------------
Section <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(Section0 / Section1 / Section2 / Section3 / Section4 / Section5) {
        return section, nil
    }

SectionTitlePrefix <- ("=")+ WS*

Section0 <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(header:(Section0Title) elements:(Section0Block*) {
        return types.NewSection(0, header.(types.SectionTitle), elements.([]interface{}))
    }) {
        return section, nil
    }

Section0TitlePrefix <- "=" WS+

Section0Title <- attributes:(ElementAttribute)* Section0TitlePrefix content:(TitleElements) WS* id:(InlineElementID)? WS* EOL { 
    return types.NewSectionTitle(content.(types.InlineElements), append(attributes.([]interface{}), id))
}

Section0Block <- !Section0 content:(Section1 / Section2 / Section3 / Section4 / Section5 / DocumentBlock) {
    return content, nil
}

Section1 <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(header:(Section1Title) elements:(Section1Block*) {
        return types.NewSection(1, header.(types.SectionTitle), elements.([]interface{}))
    }) {
    return section, nil
}

Section1TitlePrefix <- "==" WS+

Section1Title <- attributes:(ElementAttribute)* Section1TitlePrefix content:(TitleElements) WS* id:(InlineElementID)? WS* EOL { 
    return types.NewSectionTitle(content.(types.InlineElements), append(attributes.([]interface{}), id))
}

Section1Block <- !Section1Title content:(Section2 / Section3 / Section4 / Section5 / DocumentBlock) {
    return content, nil
}

Section2 <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(header:(Section2Title) elements:(Section2Block?) {
        return types.NewSection(2, header.(types.SectionTitle), elements.([]interface{}))
    }) {
        return section, nil
    }

Section2TitlePrefix <- "===" WS+ 

Section2Title <- attributes:(ElementAttribute)* Section2TitlePrefix content:(TitleElements) WS* id:(InlineElementID)? WS* EOL {
    return types.NewSectionTitle(content.(types.InlineElements), append(attributes.([]interface{}), id))
}

Section2Block <- !Section1Title !Section2Title content:(Section3 / Section4 / Section5 / DocumentBlock)* {
    return content, nil
}

Section3 <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(header:(Section3Title) elements:(Section3Block?) {
        return types.NewSection(3, header.(types.SectionTitle), elements.([]interface{}))
    }) {
        return section, nil
    }

Section3TitlePrefix <- "====" WS+ 

Section3Title <- attributes:(ElementAttribute)* Section3TitlePrefix content:(TitleElements) WS* id:(InlineElementID)? EOL {
    return types.NewSectionTitle(content.(types.InlineElements), append(attributes.([]interface{}), id))
}

Section3Block <- !Section1Title !Section2Title !Section3Title  content:(Section4 / Section5 / DocumentBlock)* {
    return content, nil
}

Section4 <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(header:(Section4Title) elements:(Section4Block?) {
        return types.NewSection(4, header.(types.SectionTitle), elements.([]interface{}))
    }) {
        return section, nil
    }

Section4TitlePrefix <- "=====" WS+ 

Section4Title <- attributes:(ElementAttribute)* Section4TitlePrefix content:(TitleElements) WS* id:(InlineElementID)? EOL {
    return types.NewSectionTitle(content.(types.InlineElements), append(attributes.([]interface{}), id))
}

Section4Block <- !Section1Title !Section2Title !Section3Title !Section4Title  content:(Section5 / DocumentBlock)* {
    return content, nil
}

Section5 <- !EOF // when reaching EOF, do not try to parse a new section again
    section:(header:(Section5Title) elements:(Section5Block?) {
        return types.NewSection(5, header.(types.SectionTitle), elements.([]interface{}))
    }) {
        return section, nil
    }

Section5TitlePrefix <- "======" WS+ 

Section5Title <- attributes:(ElementAttribute)* Section5TitlePrefix content:(TitleElements) WS* id:(InlineElementID)? EOL {
    return types.NewSectionTitle(content.(types.InlineElements), append(attributes.([]interface{}), id))
}

Section5Block <- !Section1Title !Section2Title !Section3Title !Section4Title !Section5Title content:(DocumentBlock)* {
    return content, nil
}

TitleElements <- elements:(!NEWLINE WS* !InlineElementID TitleElement WS*)+ { // absorbs heading and trailing spaces
    // fmt.Printf("matching title elements: %v\n", elements)
    return types.NewInlineElements(elements.([]interface{}))
} 

TitleElement <- element:(CrossReference / Passthrough / InlineImage / QuotedText / Link / DocumentAttributeSubstitution / Word) {
    // fmt.Printf("matching title element: %v\n", element)
    return element, nil
}


// ------------------------------------------
// Lists
// ------------------------------------------
List <- attributes:(ElementAttribute)* 
    // list items can be followed by an optional, single blank line
    elements:ListItems {
    return types.NewList(elements.([]interface{}), attributes.([]interface{}))
}

ListItems <- (OrderedListItem / UnorderedListItem / LabeledListItem)+

ListParagraph <- lines:(ListParagraphLine)+ {
    return types.NewParagraph(lines.([]interface{}), nil)
} 

ListParagraphLine <- !(OrderedListItemPrefix) 
        !(UnorderedListItemPrefix) 
        !(LabeledListItemTerm LabeledListItemSeparator) 
        !(ListItemContinuation) 
        !(ElementAttribute)
        !(BlockDelimiter)
        line:(InlineElements) {
    return line, nil
}

ListItemContinuation <- "+" WS* EOL {
    return types.NewListItemContinuation()
}

ContinuedDocumentBlock <- ListItemContinuation element:DocumentBlock {
    return element, nil
}

// ------------------------------------------
// Ordered List Items
// ------------------------------------------
OrderedListItem <- attributes:(ElementAttribute)* prefix:(OrderedListItemPrefix) content:(OrderedListItemContent) BlankLine? {
    return types.NewOrderedListItem(prefix.(types.OrderedListItemPrefix), content.([]interface{}), attributes.([]interface{}))
}

OrderedListItemPrefix <- WS* prefix:(
    // implicit numbering: '.' to '.....'
    "....." { // numbering style: "....."
        return types.NewOrderedListItemPrefix(types.UpperRoman, 5)
    } / "...." { // numbering style: "...."
        return types.NewOrderedListItemPrefix(types.UpperAlpha, 4)
    } / "..." { // numbering style: "..."
        return types.NewOrderedListItemPrefix(types.LowerRoman, 3)
    } / ".." { // numbering style: ".."
        return types.NewOrderedListItemPrefix(types.LowerAlpha, 2)
    } / "." { // numbering style: "."
        return types.NewOrderedListItemPrefix(types.Arabic, 1)
    // explicit numbering
    } / ([0-9])+ "." {  // numbering style: "1."
        return types.NewOrderedListItemPrefix(types.Arabic, 1)
    } / ([a-z])+ "." { // numbering style: "a."
        return types.NewOrderedListItemPrefix(types.LowerAlpha, 1)
    } / ([A-Z])+ "." { // numbering style: "A."
        return types.NewOrderedListItemPrefix(types.UpperAlpha, 1)
    } / ([a-z])+ ")" { // numbering style: "i)"
        return types.NewOrderedListItemPrefix(types.LowerRoman, 1)
    } / ([A-Z])+ ")" { // numbering style: "I)"
        return types.NewOrderedListItemPrefix(types.UpperRoman, 1)
    }) WS+ {
        return prefix, nil
    }

OrderedListItemContent <- elements:(ListParagraph+ ContinuedDocumentBlock*) { // Another list or a literal paragraph immediately following a list item will be implicitly included in the list item
    return types.NewListItemContent(elements.([]interface{}))
} 

// ------------------------------------------
// Unordered List Items
// ------------------------------------------
UnorderedListItem <- prefix:(UnorderedListItemPrefix) content:(UnorderedListItemContent) BlankLine? {
    return types.NewUnorderedListItem(prefix.(types.UnorderedListItemPrefix), content.([]interface{}))
}

UnorderedListItemPrefix <- WS* prefix:(
    "*****" { // ignore whitespaces, only return the relevant "*"/"-" Word
        return types.NewUnorderedListItemPrefix(types.FiveAsterisks, 5)
        } 
    / "****" { // ignore whitespaces, only return the relevant "*"/"-" Word
        return types.NewUnorderedListItemPrefix(types.FourAsterisks, 4)
        } 
    / "***" { // ignore whitespaces, only return the relevant "*"/"-" Word
        return types.NewUnorderedListItemPrefix(types.ThreeAsterisks, 3)
        } 
    / "**" { // ignore whitespaces, only return the relevant "*"/"-" Word
        return types.NewUnorderedListItemPrefix(types.TwoAsterisks, 2)
        } 
    / "*" { // ignore whitespaces, only return the relevant "*"/"-" Word
        return types.NewUnorderedListItemPrefix(types.OneAsterisk, 1)
        } 
    / "-" { // ignore whitespaces, only return the relevant "*"/"-" Word
        return types.NewUnorderedListItemPrefix(types.Dash, 1)
        }) WS+ {
    return prefix, nil
} 

UnorderedListItemContent <- elements:(ListParagraph+ ContinuedDocumentBlock*) { // Another list or a literal paragraph immediately following a list item will be implicitly included in the list item
    return types.NewListItemContent(elements.([]interface{}))
} 

// ------------------------------------------
// Labeled List Items
// ------------------------------------------
LabeledListItem <- term:(LabeledListItemTerm) LabeledListItemSeparator description:(LabeledListItemDescription) {
    return types.NewLabeledListItem(term.([]interface{}), description.([]interface{}))
} /  term:(LabeledListItemTerm) "::" WS* EOL { // here, WS is optional since there is no description afterwards
    return types.NewLabeledListItem(term.([]interface{}), nil)
}

LabeledListItemTerm <- term:(!NEWLINE !"::" .)*  {
    return term, nil
}

// term separator: ('::') and at least one space or endline
LabeledListItemSeparator <-  "::" (WS / NEWLINE)+

LabeledListItemDescription <- elements:(ListParagraph / ContinuedDocumentBlock)* { // TODO: replace with (ListParagraph+ ContinuedDocumentBlock*) and use a single rule for all item contents ?
    return types.NewListItemContent(elements.([]interface{}))
}

// ------------------------------------------
// Admonitions
// ------------------------------------------
AdmonitionKind <- "TIP" {
    return types.Tip, nil
} / "NOTE" {
    return types.Note, nil
} / "IMPORTANT" {
    return types.Important, nil
} / "WARNING" {
    return types.Warning, nil
} / "CAUTION" {
    return types.Caution, nil
}

// ------------------------------------------
// Paragraphs
// ------------------------------------------
// a paragraph is a group of line ending with a blank line (or end of file)
// a paragraph cannot start with the `section` sequence (`= `, `== `, etc.)
Paragraph <- 
    // admonition paragraph 
    attributes:(ElementAttribute)* !("="+ WS+ !NEWLINE) t:(AdmonitionKind) ": " lines:(InlineElements)+ { 
        return types.NewAdmonitionParagraph(lines.([]interface{}), t.(types.AdmonitionKind), attributes.([]interface{}))
    } / 
    // regular paragraph
    attributes:(ElementAttribute)* !("="+ WS+ !NEWLINE) lines:(InlineElements)+ { 
        return types.NewParagraph(lines.([]interface{}), attributes.([]interface{}))
    } 

InlineElements <- comment:(SingleLineComment) {
    return types.NewInlineElements([]interface{}{comment})
} / !EOF !BlockDelimiter elements:(!EOL WS* !InlineElementID InlineElement WS*)+ EOL { // absorbs heading and trailing spaces
    return types.NewInlineElements(elements.([]interface{}))
} 

InlineElement <- element:(CrossReference / Passthrough / InlineImage / QuotedText / Link / DocumentAttributeSubstitution / Word) {
    return element, nil
}

// ----------------------------------------------------------------------------
// Quoted Texts (bold, italic and monospace) including substitution prevention
// ----------------------------------------------------------------------------
QuotedText <- BoldText / ItalicText / MonospaceText /
            EscapedBoldText / EscapedItalicText / EscapedMonospaceText

BoldText <- !`\\` "**" content:(QuotedTextContent) "**" { // double punctuation must be evaluated first
    return types.NewQuotedText(types.Bold, content.([]interface{}))
} / !`\\` "**" content:(QuotedTextContent) "*" { // unbalanced `**` vs `*` punctuation
    result := append([]interface{}{"*"}, content.([]interface{}))
    return types.NewQuotedText(types.Bold, result)
} / !`\` "*" content:(QuotedTextContent) "*" { // single punctuation
    return types.NewQuotedText(types.Bold, content.([]interface{}))
} 

EscapedBoldText <- backslashes:(`\\` `\`*) "**" content:(QuotedTextContent) "**" { // double punctuation must be evaluated first
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "**", content.([]interface{}))
} / backslashes:(`\` `\`*) "**" content:(QuotedTextContent) "*" { // unbalanced `**` vs `*` punctuation
    result := append([]interface{}{"*"}, content.([]interface{}))
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "*", result)
} / backslashes:(`\` `\`*) "*" content:(QuotedTextContent) "*" { // simple punctuation must be evaluated last
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "*", content.([]interface{}))
} 

ItalicText <- !`\\` "__" content:(QuotedTextContent) "__" {
    return types.NewQuotedText(types.Italic, content.([]interface{}))
} / !`\\` "__" content:(QuotedTextContent) "_" { // unbalanced `__` vs `_` punctuation
    result := append([]interface{}{"_"}, content.([]interface{}))
    return types.NewQuotedText(types.Italic, result)
} / !`\` "_" content:(QuotedTextContent) "_" {
    return types.NewQuotedText(types.Italic, content.([]interface{}))
}

EscapedItalicText <- backslashes:(`\\` `\`*) "__" content:(QuotedTextContent) "__" { // double punctuation must be evaluated first
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "__", content.([]interface{}))
} / backslashes:(`\` `\`*) "__" content:(QuotedTextContent) "_" { // unbalanced `__` vs `_` punctuation
    result := append([]interface{}{"_"}, content.([]interface{}))
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "_", result)
} / backslashes:(`\` `\`*) "_" content:(QuotedTextContent) "_" { // simple punctuation must be evaluated last
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "_", content.([]interface{}))
} 

MonospaceText <- !`\\` "``" content:(QuotedTextContent) "``" { // double punctuation must be evaluated first
    return types.NewQuotedText(types.Monospace, content.([]interface{}))
} / !`\\` "``" content:(QuotedTextContent) "`" { // unbalanced "``" vs "`" punctuation
    result := append([]interface{}{"`"}, content.([]interface{}))
    return types.NewQuotedText(types.Monospace, result)
} / !`\` "`" content:(QuotedTextContent) "`" { // simple punctuation must be evaluated last
    return types.NewQuotedText(types.Monospace, content.([]interface{}))
}

EscapedMonospaceText <- backslashes:(`\\` `\`*) "``" content:(QuotedTextContent) "``" { // double punctuation must be evaluated first
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "``", content.([]interface{}))
} / backslashes:(`\` `\`*) "``" content:(QuotedTextContent) "`" { // unbalanced "``" vs "`" punctuation
    result := append([]interface{}{"`"}, content.([]interface{}))
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "`", result)
} / backslashes:(`\` `\`*) "`" content:(QuotedTextContent) "`" { // simple punctuation must be evaluated last
    return types.NewEscapedQuotedText(backslashes.([]interface{}), "`", content.([]interface{}))
} 

QuotedTextContent <- QuotedTextContentElement (WS+ QuotedTextContentElement)*

QuotedTextContentElement <- QuotedText / QuotedTextWord / WordWithQuotePunctuation // word with quote punctuation is only accepted if nothing matched before, so we have a chance to stop

QuotedTextWord <- (!NEWLINE !WS !"*" !"_" !"`" .)+ // cannot have "*", "_" or "`" within

WordWithQuotePunctuation <- (!NEWLINE !WS  .)+ { // can have "*", "_" or "`" within, maybe because the user inserted another quote, or made an error (extra or missing space, for example)
    return c.text, nil
}

// make sure unbalanced punctuation for quoted text is treated accordingly
UnbalancedQuotePunctuation <- "*" / "_" / "`"

// ------------------------------------------
// Passthrough
// ------------------------------------------
Passthrough <- TriplePlusPassthrough / SinglePlusPassthrough / PassthroughMacro

SinglePlusPassthrough <- "+" content:(!NEWLINE !"+" .)* "+" {
    return types.NewPassthrough(types.SinglePlusPassthrough, content.([]interface{}))
}

TriplePlusPassthrough <- "+++" content:(!"+++" .)* "+++" {
    return types.NewPassthrough(types.TriplePlusPassthrough, content.([]interface{}))
}

PassthroughMacro <- "pass:[" content:(PassthroughMacroCharacter)* "]" {
    return types.NewPassthrough(types.PassthroughMacro, content.([]interface{}))
} / "pass:q[" content:(QuotedText / PassthroughMacroCharacter)* "]" {
    return types.NewPassthrough(types.PassthroughMacro, content.([]interface{}))
}

PassthroughMacroCharacter <- (!"]" .)

// ------------------------------------------
// Cross References
// ------------------------------------------
CrossReference <- "<<" id:(ID) ">>" {
    return types.NewCrossReference(id.(string))
}

// ------------------------------------------
// Links
// ------------------------------------------
Link <- RelativeLink / ExternalLink 

ExternalLink <- url:(URL_SCHEME URL) attributes:(LinkAttributes) {
    return types.NewLink(url.([]interface{}), attributes.(map[string]interface{}))
} / url:(URL_SCHEME URL) {
    return types.NewLink(url.([]interface{}), nil)
}

// url preceeding with `link:` MUST be followed by square brackets
RelativeLink <- "link:" url:(URL_SCHEME? URL) attributes:(LinkAttributes) {
    return types.NewLink(url.([]interface{}), attributes.(map[string]interface{}))
}

LinkAttributes <- "[" text:(LinkTextAttribute)
        otherAttrs:(OtherGenericAttribute)* "]" {
    return types.NewLinkAttributes(text.([]interface{}), otherAttrs.([]interface{}))
} / "[" otherAttrs:(OtherGenericAttribute)* "]" {
    return types.NewLinkAttributes(nil, otherAttrs.([]interface{}))
}

LinkTextAttribute <- value:(!"," !"]" .)+ {
    return value, nil
}

// ------------------------------------------
// Images
// ------------------------------------------
BlockImage <- attributes:(ElementAttribute)* image:BlockImageMacro  WS* EOL {
    // here we can ignore the blank line in the returned element
    return types.NewBlockImage(image.(types.ImageMacro), attributes.([]interface{}))
}

BlockImageMacro <- "image::" path:(URL) attributes:(ImageAttributes) {
    return types.NewImageMacro(path.(string), attributes.(map[string]interface{}))
}

InlineImage <- image:InlineImageMacro {
    // here we can ignore the blank line in the returned element
    return types.NewInlineImage(image.(types.ImageMacro))
}

InlineImageMacro <- "image:" !":" path:(URL) attributes:(ImageAttributes) {
    return types.NewImageMacro(path.(string), attributes.(map[string]interface{}))
}

ImageAttributes <- "[" alt:(ImageAltAttribute)
        width:(ImageWidthAttribute)
        height:(ImageHeightAttribute)
        otherAttrs:(OtherGenericAttribute)* "]" {
    return types.NewImageAttributes(alt.([]interface{}), width.([]interface{}), height.([]interface{}), otherAttrs.([]interface{}))
} / "[" alt:(ImageAltAttribute)
        width:(ImageWidthAttribute)
        otherAttrs:(OtherGenericAttribute)* "]" {
    return types.NewImageAttributes(alt.([]interface{}), width.([]interface{}), nil, otherAttrs.([]interface{}))
} / "[" alt:(ImageAltAttribute)
        otherAttrs:(OtherGenericAttribute)* "]" {
    return types.NewImageAttributes(alt.([]interface{}), nil, nil, otherAttrs.([]interface{}))
} / "[" otherAttrs:(OtherGenericAttribute)* "]" {
    return types.NewImageAttributes(nil, nil, nil, otherAttrs.([]interface{}))
}

ImageAltAttribute <- value:(!"," !"]" .)+ {
    return value, nil
}

ImageWidthAttribute <- "," value:(!"," !"]" .)+ {
    return value, nil
}

ImageHeightAttribute <- "," value:(!"," !"]" .)+ {
    return value, nil
}

// ------------------------------------------------------------------------------------
// Delimited Blocks (http://asciidoctor.org/docs/user-manual/#built-in-blocks-summary)
// ------------------------------------------------------------------------------------
DelimitedBlock <- FencedBlock / ListingBlock / ExampleBlock / CommentBlock

BlockDelimiter <- LiteralBlockDelimiter / FencedBlockDelimiter / ListingBlockDelimiter / ExampleBlockDelimiter / CommentBlockDelimiter

FencedBlockDelimiter <- "```"

FencedBlock <- attributes:(ElementAttribute)* FencedBlockDelimiter WS* NEWLINE content:(List / BlockParagraph / BlankLine)* ((FencedBlockDelimiter WS* EOL) / EOF) {
    return types.NewDelimitedBlock(types.FencedBlock, content.([]interface{}), attributes.([]interface{}), types.None)
}

ListingBlockDelimiter <- "----"

ListingBlock <- attributes:(ElementAttribute)* ListingBlockDelimiter WS* NEWLINE content:(List / BlockParagraph / BlankLine)* ((ListingBlockDelimiter WS* EOL) / EOF) {
    return types.NewDelimitedBlock(types.ListingBlock, content.([]interface{}), attributes.([]interface{}), types.None)
}

ExampleBlockDelimiter <- "===="

ExampleBlock <- attributes:(ElementAttribute)* ExampleBlockDelimiter WS* NEWLINE content:(List / BlockParagraph / BlankLine)*  ((ExampleBlockDelimiter WS* EOL) / EOF) {
    return types.NewDelimitedBlock(types.ExampleBlock, content.([]interface{}), attributes.([]interface{}), types.None)
}

BlockParagraph <-  lines:(BlockParagraphLine)+ {
    return types.NewParagraph(lines.([]interface{}), nil)
} 

BlockParagraphLine <- !(OrderedListItemPrefix) 
        !(UnorderedListItemPrefix) 
        !(LabeledListItemTerm LabeledListItemSeparator) 
        !(ListItemContinuation) 
        !(BlockDelimiter) 
        line:(InlineElements) {
    return line, nil
}

// -------------------------------------------------------------------------------------
// Comments
// -------------------------------------------------------------------------------------

CommentBlockDelimiter <- "////"

CommentBlock <- attributes:(ElementAttribute)* CommentBlockDelimiter WS* NEWLINE content:(CommentBlockLine)*  ((CommentBlockDelimiter WS* EOL) / EOF) {
    return types.NewDelimitedBlock(types.CommentBlock, content.([]interface{}), attributes.([]interface{}), types.Verbatim)
}

CommentBlockLine <- content:(!CommentBlockDelimiter !EOL .)* EOL {
    return content, nil
}

SingleLineComment <- !CommentBlockDelimiter "//" content:(!EOL .)* EOL {
    return types.NewSingleLineComment(content.([]interface{}))
}


// -------------------------------------------------------------------------------------
// Literal Blocks (see http://asciidoctor.org/docs/user-manual/#literal-text-and-blocks)
// -------------------------------------------------------------------------------------
LiteralBlock <- ParagraphWithSpaces / ParagraphWithLiteralBlockDelimiter / ParagraphWithLiteralAttribute

// paragraph indented with one or more spaces on the first line
ParagraphWithSpaces <- spaces:(WS+) !NEWLINE content:(LiteralBlockContent) EndOfLiteralBlock {
    return types.NewLiteralBlock(spaces.([]interface{}), content.([]interface{}))
}

// no NEWLINE allowed between the first spaces and the content of the block
LiteralBlockContent <- content:(!(NEWLINE BlankLine) .)+ { 
    return content, nil
}

// a literal block ends with a blank line (or EOF)
EndOfLiteralBlock <- NEWLINE BlankLine / NEWLINE / EOF

// paragraph with the literal block delimiter (`....`)
ParagraphWithLiteralBlockDelimiter <- LiteralBlockDelimiter WS* NEWLINE content:(!LiteralBlockDelimiter .)* ((LiteralBlockDelimiter WS* EOL) / EOF) {
    return types.NewLiteralBlock([]interface{}{}, content.([]interface{}))
}

LiteralBlockDelimiter <- "...."

// paragraph with the literal attribute (`[literal]`)
ParagraphWithLiteralAttribute <- "[literal]" WS* NEWLINE content:(LiteralBlockContent) EndOfLiteralBlock {
    return types.NewLiteralBlock([]interface{}{}, content.([]interface{}))
}

// ------------------------------------------
// BlankLine
// ------------------------------------------
BlankLine <- !EOF WS* EOL {
    return types.NewBlankLine()
}

// ------------------------------------------
// Base Types
// ------------------------------------------
Word <- (!NEWLINE !WS .)+ {
    return string(c.text), nil
}

URL <- (!NEWLINE !WS !"[" !"]" .)+ {
    return string(c.text), nil
}

ID <- (!NEWLINE !WS !"[" !"]" !"<<" !">>".)+ {
    return string(c.text), nil
}

URL_TEXT <- (!NEWLINE !"[" !"]" .)+ {
    return string(c.text), nil
}

URL_SCHEME <- "http://" / "https://" / "ftp://" / "irc://" / "mailto:"

DIGIT <- [0-9]

WS <- " " / "\t" {
        return string(c.text), nil
}

NEWLINE <- "\r\n" / "\r" / "\n"

EOF <- !.

EOL <- NEWLINE / EOF